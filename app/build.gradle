import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.butterknife'

def AAVersion = '4.8.0'
def SupportLibVersion = '28.0.0'
def homePath = System.properties['user.home']
def storePasswd = System.getenv('autojspasswd')
def keyPasswd = System.getenv('autojspasswd')
def alias = System.getenv('autojsalias')
def buglyAppId = System.getenv("bugly_app_id")
def posthogAppId = System.getenv("posthog_app_id")

def signSupport = false

android {
    signingConfigs {
        if (file(homePath + '/auto-js-t-pkcs12.jks').exists()) {
            signSupport = true
            release {
                storeFile file(homePath + '/auto-js-t-pkcs12.jks')
                storePassword storePasswd
                keyPassword keyPasswd
                keyAlias = alias
            }
        }
        debug {
            storeFile file('debug/autojs-pkcs12-debug.jks')
            storePassword 'fixture'
            keyPassword 'fixture'
            keyAlias = 'autojs-debug'
        }
    }
    compileSdkVersion versions.compile
    buildToolsVersion versions.buildTool
    defaultConfig {
        applicationId "org.autojs.autojs"
        minSdkVersion versions.mini
        targetSdkVersion versions.target
        versionCode versions.appVersionCode
        versionName versions.appVersionName
        buildConfigField "String", "BUGLY_APP_ID", "\"${buglyAppId ?: ''}\""
        buildConfigField "String", "POSTHOG_APP_ID", "\"${posthogAppId ?: ''}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "resourcePackageName": android.defaultConfig.applicationId
                ]
            }
        }
        ndk { }
    }
    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".modify"
        }
        if (signSupport) {
            println('sign support')
            release.signingConfig = signingConfigs.release
        } else {
            println('sign is not support')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding = 'utf-8'
    }

    configurations.configureEach {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    // abi拆分 使用splits不知道怎么复制对应abi的template.apk到src/main/assets下
    flavorDimensions "abi"
    productFlavors {
        arm64_v8a {
            dimension "abi"
            ndk {
                abiFilters "arm64-v8a"
            }
        }
        armeabi_v7a {
            dimension "abi"
            ndk {
                abiFilters "armeabi-v7a"
            }
        }
        x86 {
            dimension "abi"
            ndk {
                abiFilters "x86"
            }
        }
        x86_64 {
            dimension "abi"
            ndk {
                abiFilters "x86_64"
            }
        }
        universal {
            dimension "abi"
            ndk {
                abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            }
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }
}

// 获取abi & buildTypes
// 调用示例: 
// def result = getAbiAndBuildType(project)
// def abi = result.abi
// def buildType = result.buildType
def getAbiAndBuildType() {
    // 获取当前编译变体
    def taskRequests = project.getGradle().getStartParameter().getTaskRequests()
    def args = taskRequests.collect { it.getArgs() }.flatten()

    // 检查 args 是否为空
    if (args.isEmpty()) {
        return [buildVariant: "assembleArm64_v8aRelease", abi: "arm64_v8a", buildType: "release"]
    }

    def buildVariant = args[0].split(':')[-1]

    // 提取 ABI 和构建类型
    def abi = buildVariant.replaceAll('assemble', '').replaceAll(/Debug|Release/, '')
    def buildType = buildVariant.replaceAll(/.*?assemble/, '').replaceAll(abi, '')

    abi = abi.toLowerCase()
    buildType = buildType.toLowerCase()

    println "selected build variant: $buildVariant"
    println "abi: $abi"
    println "build type: $buildType"

    return [buildVariant: buildVariant, abi: abi, buildType: buildType]
}

// 编译 inrt (template.apk)
def buildTemplateApk() {
    def osName = System.getProperty("os.name").toLowerCase()

    // 获取当前编译变体
    def buildArg = getAbiAndBuildType()
    def buildVariant = buildArg.buildVariant

    exec {
        println "exec gradlew inrt:${buildVariant}"
        workingDir file('../')
        println "current platform: ${osName}"
        if (osName.contains("win")) {
            commandLine 'cmd', '/c', "gradlew inrt:${buildVariant}"
        } else {
            commandLine './gradlew', "inrt:${buildVariant}"
        }
    }
}

// 复制 inrt-xxx-xxx.apk 为 src/main/assets/template.apk
def copyTemplateApk() {
    copy {
        // 获取当前编译变体
        def buildArg = getAbiAndBuildType()
        def abi = buildArg.abi
        def buildType = buildArg.buildType

        def fromFileStr = "../inrt/build/outputs/apk/${abi}/${buildType}/"
        def fileName = "inrt-${abi}-${buildType}.apk"

        def fromFile = file(fromFileStr)
        def intoFile = file("./src/main/assets/")

        println 'copy template.apk from ' + fromFileStr + fileName
        println 'into ' + intoFile

        from fromFile
        into intoFile
        include fileName
        rename fileName, 'template.apk'
    }
}

// 复制 app-xx-xx.apk
def copyFinalApk() {
    copy {
        // 获取当前编译变体
        def buildArg = getAbiAndBuildType()
        def abi = buildArg.abi
        def buildType = buildArg.buildType

        SimpleDateFormat sdf = new SimpleDateFormat("yyyyMMdd")
        def currentDate = sdf.format(new Date())

        def fromFileStr = "./build/outputs/apk/${abi}/${buildType}/"
        def intoFileStr = "../common/${buildType}/"
        def fileName = "app-${abi}-${buildType}.apk"

        def fromFile = file(fromFileStr)
        def intoFile = file(intoFileStr)

        println 'copy final apk from ' + fromFileStr + fileName
        println 'into ' + intoFileStr

        from fromFile
        into intoFile
        include fileName
        rename fileName, "${versions.patchName}-${abi}-${buildType}-${currentDate}.apk"
    }
}

tasks.register('buildAndCopyTemplateApk') {
    doFirst {
        buildTemplateApk()
    }
    doLast {
        copyTemplateApk()
    }
}

tasks.register('copyFinalApks') {
    doLast {
        copyFinalApk()
    }
}

// 在执行 preBuild 任务之前, 编译复制 template.apk
preBuild.dependsOn 'buildAndCopyTemplateApk'

// 在整个 build 完成后执行任务
tasks.configureEach { task ->
    def taskName = task.name
    if (taskName.contains("assemble")) {
        task.finalizedBy "copyFinalApks"
    }
}


repositories {
    google()
}

dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // TensorFlow-lite
//    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1-alpha01', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$junit_version"
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
    // Android Annotations
    kapt "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    // ButterKnife
    implementation('com.jakewharton:butterknife:10.2.3', {
        exclude group: 'com.android.support'
    })
    kapt 'com.jakewharton:butterknife-compiler:10.2.3'
    // Android supports
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$appcompat_version"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "com.google.android.material:material:$material_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    // old version support
    implementation "com.android.support:appcompat-v7:${SupportLibVersion}"
    implementation "com.android.support:support-v4:${SupportLibVersion}"
    // Personal libraries
    implementation 'com.github.hyb1996:MutableTheme:1.0.0'
    // Material Dialogs
    implementation('com.afollestad.material-dialogs:core:0.9.2.3', {
        exclude group: 'com.android.support'
    })
    // Common Markdown
    implementation 'com.github.atlassian:commonmark-java:commonmark-parent-0.9.0'
    // Android issue reporter (a github issue reporter)
    implementation('com.heinrichreimersoftware:android-issue-reporter:1.3.1', {
        exclude group: 'com.afollestad.material-dialogs'
        exclude group: 'com.android.support'
    })
    //MultiLevelListView
    implementation 'com.github.hyb1996:android-multi-level-listview:1.1'
    //Licenses Dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    //Expandable RecyclerView
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'
    //FlexibleDivider
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    //???
    implementation 'com.wang.avi:library:2.1.3'
    //Commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    //Expandable RecyclerView
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'
    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    //Glide
    implementation('com.github.bumptech.glide:glide:4.12.0', {
        exclude group: 'com.android.support'
    })
    kapt 'com.github.bumptech.glide:compiler:4.15.1'
    //joda time
    implementation 'joda-time:joda-time:2.12.5'
    // Tasker Plugin
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:4.0.3'
    // PostHog
    implementation("com.posthog:posthog-android:3.1.15")
    // Bugly
    implementation 'com.tencent.bugly:crashreport:4.0.4'
    // 4.+ 合并了 不需要手动引入
    // implementation 'com.tencent.bugly:nativecrashreport:3.9.2'
    // MaterialDialogCommon
    implementation('com.afollestad.material-dialogs:commons:0.9.6.0', {
        exclude group: 'com.android.support'
    })
    // WorkManager
    implementation 'androidx.work:work-runtime:2.8.1'
    // Android job
    implementation 'com.evernote:android-job:1.4.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'
    implementation project(':automator')
    implementation project(':common')
    implementation project(':autojs')
    implementation project(':apkbuilder')
    implementation 'com.github.TimScriptov:apksigner:1.2.0'
}
